# This policy restricts all instances of the aws_instance resource
# in the plan to have all tags in the mandatory_tags list.

# It uses the following functions:
## validate_top_level_resource_attribute_contains_list
## find_resources_from_plan

### imports ###
import "tfplan"
import "strings"

### validate_top_level_resource_attribute_contains_list ###
# This function validates that a specific top-level attribute
# that is a map or list contains all members of a given list
# for all instances of a specific resource in all modules

validate_top_level_resource_attribute_contains_list = func(resource_type, attribute, required_values) {

  # Arguments
  ## resource_type: the type of resource to validate
  ## attribute: a top-level attribute of the resource that is a map or list
  ## required_values: a list of required values for the attribute

  # Get all resources of the specified type
  list_of_resource_maps = find_resources_from_plan(resource_type)

  # Extract the maps from the main list
  for list_of_resource_maps as resource_map {
    # For each map, get the name and map of resources
    for resource_map as name, resources {
      # Get each resource instance
      for resources as _, r {

        # Skip resources that are being destroyed
        if length(r.diff) == 0 {
          print("Skipping resource", name, "that is being destroyed.")
          continue
        }

        # Validate that the attribute is not computed.
        # We check "attribute.%" and "attribute.#" because an
        # attribute of type map or list won't show up in the diff
        if (r.diff[attribute + ".%"].computed else false) or
        (r.diff[attribute + ".#"].computed else false) {
          print("resource", name, "has a computed attribute:", attribute)
          return false
        }

        # Validate that the attribute exists
        if (length(r.applied[attribute]) else 0) > 0 {
          applied_value = r.applied[attribute]
        } else {
          print("resource", name, "is missing attribute:", attribute)
          return false
        }

        # Get each member of the required_values list
        for required_values as rv {
          # Test that the attribute contains the member of the list
          if applied_value not contains rv {
            # Print name of first violator along with
            # attribute, required value, and the list
            print("resource", name,
              "has attribute", attribute,
              "that is missing required value", rv,
              "from the list:", required_values)
            return false
          }
        }
      }
    }
  }

  print("No violators of type", resource_type, "for attribute",
    attribute, "against the list:", required_values)
  return true
}

### find_resources_from_plan ###
# This function gets all instances of a specific resource
# from all modules using the tfplan import

find_resources_from_plan = func(r) {

  # Arguments
  ## r: the type of resource to find

  # Start with an empty map
  resources = []

  # Iterate over all modules in the tfplan import
  for tfplan.module_paths as path {
    # Append all resources of the specified type
    # If a module does not have any resources of the
    # specified type in the plan, we append the empty map, {}.
    append(resources, (tfplan.module(path).resources[r] else {}))
  }

  return resources
}

### List of mandatory tags ###
mandatory_tags = [
  "Name",
  "ttl",
  "owner",
]

### Rule to enforce mandatory tags ###
check_mandatory_tags = rule {
  validate_top_level_resource_attribute_contains_list(
    "aws_instance",
    "tags",
    mandatory_tags)
}

### main rule that requires other rules to be true ###
main = rule {
  (check_mandatory_tags) else true
}
